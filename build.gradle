plugins {
  id 'java'
  id 'maven-publish'
  id 'java-library'
}

targetCompatibility = '1.8'

group 'com.vmware.gemfire'
version '2.0.0-SNAPSHOT'

repositories {
  mavenCentral()

  maven {
    name = 'VMware Repository'
    url = project.findProperty("commercialRepository") ?: "https://commercial-repo.pivotal.io/data3/gemfire-release-repo/gemfire"
    credentials {
      username = System.getenv("COMMERCIAL_MAVEN_USERNAME")
      password = System.getenv("COMMERCIAL_MAVEN_PASSWORD")
    }
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      pom {
        name = "${project.group}:${project.name}"
        description = 'Provide integration of GemFire and Testcontainers'
        url = 'https://github.com/gemfire/gemfire-testcontainers'
        developers {
          developer {
            name = 'Jens Deppe'
            email = 'jdeppe@vmware.com'
            organization= 'VMware'
            organizationUrl = 'https://vmware.com'
          }
        }
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        scm {
          url = 'https://github.com/gemfire/gemfire-testcontainers'
          connection = 'scm:git:git://github.com/gemfire/gemfire-testcontainers.git'
          developerConnection = 'scm:git:ssh://github.com:gemfire/gemfire-testcontainers.git'
        }
      }

      from components.java
    }
  }

  repositories {
    maven {
      name = "VMware"
      url = project.findProperty("publishRepository")
      credentials {
        username = System.getenv("PUBLISH_MAVEN_USERNAME")
        password = System.getenv("PUBLISH_MAVEN_PASSWORD")
      }
    }
  }
}

dependencies {
  api 'org.testcontainers:testcontainers:1.18.3'

  testImplementation 'org.assertj:assertj-core:3.24.2'
  testImplementation 'ch.qos.logback:logback-classic:1.3.5'
  testImplementation 'com.vmware.gemfire:gemfire-core:10.0.1'

  testRuntimeOnly 'com.vmware.gemfire:gemfire-log4j:10.0.1'
  testRuntimeOnly 'com.vmware.gemfire:gemfire-logging:10.0.1'
  testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.19.0'
}

java {
  withSourcesJar()
  withJavadocJar()
}

test {
  useJUnit()
  boolean debugTests = project.findProperty("logContainerOutput") ?: false
  jvmArgs "-Dgemfire-testcontainers.log-container-output=${debugTests}"

  if (project.findProperty('gemfire.image') != null) {
    systemProperty 'gemfire.image', findProperty('gemfire.image')
  }
}
