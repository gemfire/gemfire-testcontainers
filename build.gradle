plugins {
  id 'java'
  id 'maven-publish'
  id 'java-library'
  id 'signing'
}

targetCompatibility = '1.8'

group 'com.vmware.gemfire'
version '1.0-SNAPSHOT'

repositories {
  mavenCentral()

  maven {
    name = 'PivotalRepository'
    url = uri('https://commercial-repo.pivotal.io/data3/gemfire-release-repo/gemfire')
    credentials {
      username = System.getenv("COMMERCIAL_MAVEN_USERNAME")
      password = System.getenv("COMMERCIAL_MAVEN_PASSWORD")
    }
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      pom {
        name = "${project.group}:${project.name}"
        description = 'Provide integration of GemFire and Testcontainers'
        url = 'https://github.com/gemfire/gemfire-testcontainers'
        developers {
          developer {
            name = 'Jens Deppe'
            email = 'jdeppe@vmware.com'
            organization= 'VMware'
            organizationUrl = 'https://vmware.com'
          }
        }
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        scm {
          url = 'https://github.com/gemfire/gemfire-testcontainers'
          connection = 'scm:git:git://github.com/gemfire/gemfire-testcontainers.git'
          developerConnection = 'scm:git:ssh://github.com:gemfire/gemfire-testcontainers.git'
        }
      }

      from components.java
    }
  }

  repositories {
    maven {
      name = "OSSRH"
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = System.getenv("MAVEN_USERNAME")
        password = System.getenv("MAVEN_PASSWORD")
      }
    }
  }
}

dependencies {
  implementation 'org.testcontainers:testcontainers:1.17.6'

  testImplementation 'org.assertj:assertj-core:3.24.2'
  testImplementation 'ch.qos.logback:logback-classic:1.3.5'
  testImplementation 'com.vmware.gemfire:gemfire-core:10.0.0'
}

java {
  withSourcesJar()
  withJavadocJar()
}

test {
  useJUnit()
}

signing {
  def signingKey = findProperty("signingKey")
  def signingPassword = findProperty("signingPassword")
  useInMemoryPgpKeys(signingKey, signingPassword)
  sign publishing.publications.maven
}